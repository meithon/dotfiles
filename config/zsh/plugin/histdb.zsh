_zsh_autosuggest_strategy_histdb_top() {
  local query="
    select commands.argv from history
    left join commands on history.command_id = commands.rowid
    left join places on history.place_id = places.rowid
    where commands.argv LIKE '$(sql_escape $1)%'
    group by commands.argv, places.dir
    order by places.dir != '$(sql_escape $PWD)', count(*) desc
    limit 1
  "
  suggestion=$(_histdb_query "$query")
}

ZSH_AUTOSUGGEST_STRATEGY=histdb_top

_per_directory_history_is_global=0

per-directory-history-toggle-history() {
  # ã‚°ãƒ­ãƒ¼ãƒãƒ«å¤‰æ•°ã®åˆæœŸåŒ–
  if [[ -z "$_per_directory_history_is_global" ]]; then
    _per_directory_history_is_global=0
  fi

  # ç¾åœ¨ã®ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã®ãƒ’ã‚¹ãƒˆãƒªãƒ¼ãƒ•ã‚¡ã‚¤ãƒ«
  local directory_history_file="${HOME}/.zsh_history_${PWD//\//_}"

  if [[ $_per_directory_history_is_global -eq 0 ]]; then
    # ãƒ­ãƒ¼ã‚«ãƒ«â†’ã‚°ãƒ­ãƒ¼ãƒãƒ«åˆ‡ã‚Šæ›¿ãˆ
    _per_directory_history_is_global=1
    local msg="ðŸ“ History Mode: Global"
  else
    # Switch from global to directory-specific history
    _per_directory_history_is_global=0
    local msg="ðŸ“‚ History Mode: Directory-specific"
  fi

  print "$msg"
  print
  zle reset-prompt
}

zle -N per-directory-history-toggle-history
bindkey -M vicmd '^v' per-directory-history-toggle-history
bindkey -M viins '^v' per-directory-history-toggle-history

function search_history() {
  local sql_query
  if [[ -z "$_per_directory_history_is_global" ]] || [[ $_per_directory_history_is_global -eq 1 ]]; then
    sql_query="
      SELECT commands.argv 
      FROM commands 
      INNER JOIN history ON history.command_id = commands.rowid
      WHERE commands.argv != ''
      GROUP BY commands.argv
      ORDER BY MAX(history.start_time) DESC, COUNT(*) DESC
    "
  else
    sql_query="
      SELECT commands.argv 
      FROM commands 
      INNER JOIN history ON history.command_id = commands.rowid
      INNER JOIN places ON history.place_id = places.rowid
      WHERE places.dir = '$(sql_escape $PWD)'
        AND commands.argv != ''
      GROUP BY commands.argv
      ORDER BY MAX(history.start_time) DESC, COUNT(*) DESC
    "
  fi

  local fzf_preview_cmd='echo {}| syncat -l bash'

  # é…åˆ—ã‚’ä½¿ç”¨ã™ã‚‹æ–¹æ³•
  local -a fzf_opts=(
    --query="$BUFFER"
    --read0
    --ansi
    --layout=reverse
    --no-sort
    --height=40%
    --highlight-line
    --tiebreak=chunk,length,index
    --preview='echo {}| syncat -l bash'
    --preview-window=down:20%:wrap
    --prompt='History > '
    --header='[CTRL-Y:copy, CTRL-R:execute, ESC:exit]'
    --bind 'ctrl-y:execute-silent(echo -n {} | pbcopy)+abort'
    --bind 'ctrl-r:execute(echo {} | sh)+abort'
    --bind 'ctrl-v:execute(per-directory-history-toggle-history)'
    --bind 'esc:abort'
    --bind 'ctrl-/:toggle-preview'
    --color='header:italic:underline'
  )

  local history_data="$(
    _histdb_query -json "$sql_query"
  )"
  local processed_data
  processed_data=$(
    jq -j '.[].argv | gsub("\n$";"") + "\u0000"' <<<"$history_data" |
      perl -pe 's/\x00/;\n\x1E/g' |
      perl -pe 's/\x1E//' |
      syncat --language bash |
      perl -0777 -pe 's/;\n+/\x00/g'
  )

  local selected_command="$(echo "$processed_data" | fzf "${fzf_opts[@]}")"

  BUFFER=$(echo $selected_command)
  CURSOR=$#BUFFER
}

zle -N search_history
function zvm_after_init() {
  bindkey '^R' search_history
  bindkey -M vicmd '^R' search_history
  bindkey -M viins '^R' search_history
}
unsetopt HIST_REDUCE_BLANKS # ç©ºç™½ã®å‰Šé™¤ã‚’ç„¡åŠ¹åŒ–ï¼ˆæ”¹è¡Œã‚’ä¿æŒã—ãŸã„å ´åˆï¼‰
